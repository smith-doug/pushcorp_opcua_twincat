<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FB_Spindle" Id="{eefa1b23-964a-4427-b06c-af0370dca8a6}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Spindle
VAR_INPUT
	Enable	: BOOL; // Enable the servo
	SetVelo	: REAL;	// Speed in RPMs
END_VAR
VAR_IN_OUT
	data	: ST_Spindle;	// Ethernet IP data
END_VAR
VAR_OUTPUT
	ActVelo	: REAL;	//Speed in RPMs
	Ready	: BOOL; // Servo ready to move
	Error	: UINT; 
END_VAR
VAR
	rTargetVel	: REAL;
	
	fbTrigEnable	: BOOL;
	
	iState	: DINT;
	
	fbDelay	: TON;
	bRunDelay	: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT Enable THEN
	iState := 0;
END_IF
// Scale target rpm
rTargetVel := SCALE_R(
				X:= SetVelo, 
				I_LO:= -15000, 
				I_HI:= 15000, 
				O_LO:= -30000, 
				O_HI:= 30000);
				
fbDelay(IN:= bRunDelay, PT:= T#1000MS, Q=> , ET=> );				

CASE iState OF
0:	// Default, set all output to 0
	MEMSET(ADR(data.out.flags), 0, SIZEOF(data.out.flags));
	bRunDelay := FALSE;
	IF Enable THEN
		iState := 10;
	END_IF
10:
	IF data.in.flags.DriveError THEN
		data.out.flags.ResetFault := TRUE;
		iState := 20;	// Reset state
	ELSE			
		iState := 100;	// OK to run
	END_IF	
20:
	data.out.flags.ServoEnable := FALSE;
	data.out.flags.ResetFault := data.in.flags.DriveError;
	bRunDelay := TRUE;	// Don't try to power on for at least a second
	IF NOT data.in.flags.DriveError AND fbDelay.Q THEN
		iState := 100;
		bRunDelay := FALSE;
	END_IF
	
100:
	data.out.flags.ServoEnable := TRUE;
	data.out.SpindleSpeed := TO_INT(rTargetVel);
	
	IF data.in.flags.DriveError THEN
		iState := 999;
	END_IF
	
999: // Stay in fault state until error can be examined
	data.out.flags.ServoEnable := FALSE;
	;
END_CASE


Error := TO_UINT(data.in.flags.DriveError);



IF data.in.flags.RpmLT5 THEN
	ActVelo := 0;
ELSE
	ActVelo := SCALE_R(
		X:= data.in.SpindleSpeed, 
		I_LO:= -30000, 
		I_HI:= 30000, 
		O_LO:= -15000, 
		O_HI:= 15000);
END_IF

	]]></ST>
    </Implementation>
    <Method Name="OpcuaMonitor" Id="{563c18c7-eb09-469e-b42e-c17efa116a0d}">
      <Declaration><![CDATA[METHOD OpcuaMonitor : BOOL
VAR_INPUT
END_VAR
VAR_IN_OUT
	Data	: ST_SpindleControl;
END_VAR
VAR_OUTPUT
	Error	: BOOL;
END_VAR
VAR_INST
	fbHbMonitor	: FB_HeartbeatMonitor;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbHbMonitor(CLK:= Data.status.input.Heartbeat, PT:= T#2.5S, Q=> );

IF NOT fbHbMonitor.Q THEN
	MEMSET(ADR(Data.input), 0, SIZEOF(Data.input));
	Error := TRUE;
ELSE
	Error := FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Spindle">
      <LineId Id="100" Count="2" />
      <LineId Id="122" Count="5" />
      <LineId Id="121" Count="0" />
      <LineId Id="128" Count="1" />
      <LineId Id="103" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="95" Count="0" />
      <LineId Id="105" Count="0" />
      <LineId Id="199" Count="0" />
      <LineId Id="106" Count="0" />
      <LineId Id="114" Count="1" />
      <LineId Id="107" Count="1" />
      <LineId Id="138" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="166" Count="0" />
      <LineId Id="169" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="170" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="200" Count="0" />
      <LineId Id="139" Count="1" />
      <LineId Id="201" Count="0" />
      <LineId Id="141" Count="0" />
      <LineId Id="118" Count="2" />
      <LineId Id="130" Count="0" />
      <LineId Id="192" Count="0" />
      <LineId Id="194" Count="1" />
      <LineId Id="193" Count="0" />
      <LineId Id="131" Count="1" />
      <LineId Id="196" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="96" Count="0" />
      <LineId Id="94" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="87" Count="1" />
      <LineId Id="81" Count="0" />
      <LineId Id="69" Count="2" />
      <LineId Id="73" Count="0" />
      <LineId Id="75" Count="4" />
      <LineId Id="74" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="47" Count="0" />
    </LineIds>
    <LineIds Name="FB_Spindle.OpcuaMonitor">
      <LineId Id="5" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="16" Count="1" />
      <LineId Id="23" Count="2" />
      <LineId Id="18" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>